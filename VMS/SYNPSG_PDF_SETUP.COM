$ !
$ SET NOVERIFY
$ !
$ ! Switch the comments on VERSION if necessary
$ !
$ !VERSION = "953B"
$ VERSION = "1031"
$ !
$ IF (F$GETSYI("ARCH_NAME").EQS."IA64")
$ THEN ARCH="IA64"
$ ELSE ARCH="AXP"
$ ENDIF
$ !
$ ROOT = F$PARSE(F$ENVIRONMENT("PROCEDURE"),,,"DEVICE") + F$PARSE(F$ENVIRONMENT("PROCEDURE"),,,"DIRECTORY")
$ SET DEFAULT 'ROOT
$ !
$ IF F$SEARCH("HDR.DIR") .EQS. "" THEN CREATE/DIR [.HDR]
$ IF F$SEARCH("OBJ.DIR") .EQS. "" THEN CREATE/DIR [.OBJ]
$ IF F$SEARCH("OUT.DIR") .EQS. "" THEN CREATE/DIR [.OUT]
$ !
$ SRCDIR = F$EXTRACT(0,F$LOCATE("]",ROOT),ROOT) + ".-.SOURCE.PDF]"
$ EGSDIR = F$EXTRACT(0,F$LOCATE("]",ROOT),ROOT) + ".-.SOURCE.EXAMPLES]"
$ HDRDIR = F$EXTRACT(0,F$LOCATE("]",ROOT),ROOT) + ".HDR]"
$ OBJDIR = F$EXTRACT(0,F$LOCATE("]",ROOT),ROOT) + ".OBJ]"
$ TARGET = F$EXTRACT(0,F$LOCATE("]",ROOT),ROOT) + ".''ARCH'.''VERSION']"
$ OUTDIR = F$EXTRACT(0,F$LOCATE("]",ROOT),ROOT) + ".OUT]"
$ IMGDIR = F$EXTRACT(0,F$LOCATE("]",ROOT),ROOT) + ".-.MEDIA]"
$ !
$ DEFINE/NOLOG PDF_ROOT     'ROOT
$ DEFINE/NOLOG PDF_SRC      'SRCDIR
$ DEFINE/NOLOG PDF_EG_SRC   'EGSDIR
$ DEFINE/NOLOG PDF_OBJ      'OBJDIR
$ DEFINE/NOLOG SYNEXPDIR    'HDRDIR
$ DEFINE/NOLOG SYNIMPDIR    'HDRDIR
$ DEFINE/NOLOG PDF_EXE      'TARGET
$ DEFINE/NOLOG OUTPUT       'OUTDIR
$ DEFINE/NOLOG MEDIA        'IMGDIR
$ !
$ DEFINE/NOLOG LIBHPDF      PDF_EXE:LIBHPDF.EXE
$ DEFINE/NOLOG PDFDBL       PDF_EXE:PDFDBL.EXE
$ DEFINE/NOLOG SYNPSGPDF    PDF_EXE:SYNPSG_PDF.EXE
$ !
$ IF F$SEARCH(F$TRNLNM("LIBHPDF")) .EQS. ""
$ THEN
$   WRITE SYS$OUTPUT ""
$   WRITE SYS$OUTPUT "ERROR: Use EXTRACT.COM to extract the required HARU PDF libraries, and check"
$   WRITE SYS$OUTPUT "that VERSION is correctly set in SYNPSG_PDF_SETUP.COM"
$   WRITE SYS$OUTPUT ""
$ ENDIF
$ !
$ EXIT
