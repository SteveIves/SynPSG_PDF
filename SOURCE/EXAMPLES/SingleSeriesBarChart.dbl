
import SynPSG.PDF

subroutine SingleSeriesBarChart
    record
        pdf,        @PdfFile
        chartData,  @ChartDataSet
        bottom,     XYcoordinate
    endrecord
proc
    ;;Create a new PDF document
    pdf = new PdfFile(,,,,,,,,,,,"OUTPUT:")

    ;;Override default name to suppress date/time prefix, and and ask reader
    ;;apps to display the file in full page view.
    pdf.FileName = "SingleSeriesBarChart.pdf"
    pdf.PageLayout = PdfPageLayout.Single

    ;;Add a title
    pdf.DrawText("Sales YTD vs Recent Years",50,pdf.Height-70,PdfColor.DarkCandyapplered,"Courier-Bold",24)

    ;;Configure a data set for a single series of data containing four items
    chartData = new ChartDataSet(1,4)

    ;;Populate the data set
    chartData.Series[1].Items[1] = new ChartItem(10,"Sales three years ago ",PdfColor.Red)
    chartData.Series[1].Items[2] = new ChartItem(20,"Sales two years ago   ",PdfColor.Orange)
    chartData.Series[1].Items[3] = new ChartItem(30,"Sales one year ago    ",PdfColor.Yellow)
    chartData.Series[1].Items[4] = new ChartItem(40,"Sales so far this year",PdfColor.Green)
    
    ;;Draw a bar chart
    pdf.DrawBarChart(50,pdf.Height-100,pdf.Width-50,pdf.Height-400,10,chartData,PdfColor.LightBlue)

    ;Add a chart legend and draw a box around it
    bottom = pdf.DrawSingleSeriesChartLegend(55,pdf.Height-430,chartData,true)
    pdf.DrawBox(50,pdf.Height-425,bottom.X+5,bottom.Y-5)

    ;;Save the document and show the results
    pdf.Preview()

    xreturn

endsubroutine

