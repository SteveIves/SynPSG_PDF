
import SynPSG.PDF
import Synergex.SynergyDE.Select

subroutine DblMessagesReport

    ;;Layout of DBLDIR:syntxt.ism
    record syntxt   ;218
        facility,   a3
        errnum,     d5
        errcode,    a10
        severity,   a1
        message,    a199
    endrecord

    ;;Output line format for our PDF report (with space for vertical lines)
    record printLine
        ,           a2
        facility,   a3
        ,           a3
        errnum,     a5
        ,           a3
        errcode,    a10
        ,           a3
        severity,   a1
        ,           a3
        message,    a97
        ,           a2
    endrecord

    record
        pdf,        @PdfFile
        row,        int
        maxrow,     int
    endrecord

proc

    ;;Create PDF document, 132 columns in landscape mode, 2 header and 2 footer lines
    pdf = new PdfFile(,PdfOrientation.Landscape,132,,,,,,,2,2,"OUTPUT:")

    ;;Override default file name (suppresses date/time prefix)
    pdf.FileName = "DBLMessages.pdf"

    ;;Setup header and footer content
    pdf.HeaderText[1] = "Synergy Message File Report                                                                              Printed MM/DD/YYYY at HH:MM"
    pdf.FooterText[2] = "Page [PAGE#]"

    ;------------------------------------------------------------------------------------------------------------------------------------
    ;| XXX | XXXXX | XXXXXXXXXX | X | XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX |
    ;------------------------------------------------------------------------------------------------------------------------------------

    ;;Add "auto-items" to draw the lines, boxes and column headings on each page
    pdf.AutoItems.Add(new AutoBoxRowCol(1,1,pdf.Rows,pdf.Columns))                      ;Outer box
    pdf.AutoItems.Add(new AutoTextRowCol("Fac   Err #   Code        Sev  Message",2,3)) ;Column headings
    pdf.AutoItems.Add(new AutoLineRowCol(3,1,3,pdf.Columns))                            ;Line below headings
    pdf.AutoItems.Add(new AutoLineRowCol(1,7,pdf.Rows,7))                               ;Vertical lines between columns
    pdf.AutoItems.Add(new AutoLineRowCol(1,15,pdf.Rows,15))                             ;
    pdf.AutoItems.Add(new AutoLineRowCol(1,28,pdf.Rows,28))                             ;
    pdf.AutoItems.Add(new AutoLineRowCol(1,32,pdf.Rows,32))                             ;

    ;;Start displaying data on row 4 (allowing for the headings and lines in rows 1 to 3)
    row = 4

    ;Stop one line before the bottom because of the bottom line
    maxrow = pdf.Rows - 1
    
    ;;Read the ISAM file and create the report
    foreach syntxt in new Select(new From("DBLDIR:syntxt.ism",syntxt))
    begin
        ;;Setup the print line
        init printLine
        printLine.facility = syntxt.facility
        printLine.errnum = %string(syntxt.errnum,"ZZZZX")
        printLine.errcode = syntxt.errcode        
        using printLine.facility select
        ("CMP","LBR","LNK","RNT"),
        begin
            printLine.severity = syntxt.severity
            printLine.message = syntxt.message
        end
        (),
        begin
            printLine.severity = " "
            printLine.message = syntxt.severity + syntxt.message
        end
        endusing
        
        ;;Add the line to the PDF
        pdf.DrawTextRC(printLine,row,1)

        ;;Do we have more room on the page?
        if (row<maxrow) then
            row += 1
        else
        begin
            ;;Nope - page is full, start a new one
            pdf.NewPage()
            row = 4
        end
        
    end
    
    ;;All done, show the results
    pdf.Preview()

    xreturn

endsubroutine

