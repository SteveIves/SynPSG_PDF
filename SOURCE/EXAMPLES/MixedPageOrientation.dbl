
import SynPsg.PDF

subroutine MixedPageOrientation
    record
        pdf, @PdfFile
    endrecord
proc
    ;;Create a new PDF document
    pdf = new PdfFile(,,,,,,,,,,,"OUTPUT:")

    ;;Override default name to suppress date/time prefix, and and ask reader
    ;;apps to display the file in full page view.
    pdf.FileName = "MixedPageOrientation.pdf"
    pdf.PageLayout = PdfPageLayout.Single
    pdf.PageMode = PdfPageMode.Outline
    pdf.OutlineRootText = "Mixed Page Orientation Document"

    ;;Add page one content
    pdf.NewPage(,"Portrait page")
    pdf.DrawTextRC("This page is portrait",,,,,,true)
    pdf.DrawTextRC("The page is " + %string(pdf.Width,"XXX.X") + " pixels wide and " + %string(pdf.Height,"XXX.X") + " pixels high.",,,,,,true)
    pdf.DrawTextRC("The page body is " + %string(pdf.Columns) + " columns wide and " + %string(pdf.Rows) + " rows high.")

    ;;Add a new landscape page and some content
    pdf.NewPage(PdfOrientation.Landscape,"Landscape page")
    pdf.DrawTextRC("This page is landscape",,,,,,true)
    pdf.DrawTextRC("The page is " + %string(pdf.Width,"XXX.X") + " pixels wide and " + %string(pdf.Height,"XXX.X") + " pixels high.",,,,,,true)
    pdf.DrawTextRC("The page body is " + %string(pdf.Columns) + " columns wide and " + %string(pdf.Rows) + " rows high.")

    ;;Add a portrait page and some content
    pdf.NewPage(,"Another portrait page")
    pdf.DrawTextRC("This page is portrait",,,,,,true)
    pdf.DrawTextRC("The page is " + %string(pdf.Width,"XXX.X") + " pixels wide and " + %string(pdf.Height,"XXX.X") + " pixels high.",,,,,,true)
    pdf.DrawTextRC("The page body is " + %string(pdf.Columns) + " columns wide and " + %string(pdf.Rows) + " rows high.")

    ;;Save the document and show the results
    pdf.Preview()

    xreturn

endsubroutine

