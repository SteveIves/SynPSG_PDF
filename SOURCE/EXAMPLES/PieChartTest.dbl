
import SynPSG.PDF

subroutine PieChartTest
    record
        pdf, @PdfFile
        slices, @ChartDataSet
        bottom, XYcoordinate
    endrecord
proc
    ;;Create a new PDF document
    pdf = new PdfFile(,,,,,,,,,,,"OUTPUT:")

    ;;Override default name to suppress date/time prefix, and and ask reader
    ;;apps to display the file in full page view.
    pdf.FileName = "PieChartTest.pdf"
    pdf.PageLayout = PdfPageLayout.Single

    ;;Add a title
    pdf.DrawText("Sales YTD vs Recent Years",50,pdf.Height-100,PdfColor.DarkCandyapplered,"Courier-Bold",24)
    
    ;;Define the slices of the pie
    slices = new ChartDataSet(1,4)
    slices.Series[1].Items[1] = new ChartItem(10,"Sales three years ago     ",PdfColor.Red)
    slices.Series[1].Items[2] = new ChartItem(20,"Sales two years ago       ",PdfColor.Orange)
    slices.Series[1].Items[3] = new ChartItem(30,"Sales one year ago        ",PdfColor.Yellow)
    slices.Series[1].Items[4] = new ChartItem(40,"Sales current year to date",PdfColor.Green)

    ;;Draw a pie chart
    pdf.DrawPieChart(250,pdf.Height-330,200,slices,PdfColor.LightBlue)
    
    ;Add a chart legend and draw a box around it
    bottom = pdf.DrawSingleSeriesChartLegend(55,230,slices,true)
    pdf.DrawBox(50,235,bottom.X+5,bottom.Y-5)

    ;;Save the document and show the results
    pdf.Preview()

    xreturn

endsubroutine

